// this bit of complexity is to pull down the main build script from the alclabs maven repository
// and then apply it.
configurations { gradleScript }
repositories { maven { url = 'http://repo.alcshare.com' } }
dependencies { gradleScript group: 'com.alcshare', name: 'alclabs-gradle', ext: 'gradle', version: '1.22' }
//dependencies { gradleScript group: 'com.alcshare', name: 'addon-gradle', ext: 'gradle', version: '1.6' }
apply from: configurations.gradleScript.resolve().iterator().next()

info {
  name = 'Proxy Configuration'
  description = 'Sets up system proxy information'
  version = '1.0 Prerelease '+ new Date().format('yyyy/MM/dd kk:mm:ss')
  vendor = 'ALC Labs'
}

/* have to fork compiler to get it to ignore the symbol file.  Need to use private sun
   classes like NTLMAuthenticationCallback to control proxy authentication appropriately */
compileJava {
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    compile files('lib/proxy-vole_20131209.jar')
    compile 'org.json:json:20141113'
    compile 'commons-codec:commons-codec:1.10'
//    compile 'commons-lang:commons-lang:2.6'
    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'com.controlj.green:addonsupport-api-addon:1.3.0'
    providedCompile 'com.controlj.green:directaccess-api-addon:1.3.0'

    testCompile 'org.spockframework:spock-core:0.6-groovy-1.7'
    testCompile 'junit:junit:4.8.1'
    testCompile 'cglib:cglib-nodep:2.2'
}

// on *nix the expanded will have read only files in it
task cleanExpanded(type: Delete) {
    delete file('build/tmp/expandedArchives');
}
deploy.dependsOn cleanExpanded

// required for compatibility with 4.1 SP1b
sourceCompatibility = 1.5
targetCompatibility = 1.5
